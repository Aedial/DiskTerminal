apply from: 'gradle/scripts/helpers.gradle'

setDefaultProperty('publish_to_maven', true, false)
setDefaultProperty('publish_to_curseforge', true, false)
setDefaultProperty('publish_to_modrinth', true, false)

// CI can override these via -P flags to support RC/pre-release tags where the changelog
// section doesn't match mod_version (e.g. mod_version=1.4.0, tag=v1.4.0-rc1).
// - curseforge_changelog: raw markdown changelog content (bypasses parserChangelog())
// - curseforge_display_name: override CurseForge display name (default: "Mod Name mod_version")
setDefaultProperty('curseforge_changelog', false, '')
setDefaultProperty('curseforge_display_name', false, '')

if (propertyBool('publish_to_maven')) {
    assertProperty('maven_name')
    assertProperty('maven_url')
    publishing {
        repositories {
            maven {
                name propertyString('maven_name').replaceAll("\\s", "")
                url propertyString('maven_url')
                credentials(PasswordCredentials)
            }
        }
        publications {
            mavenJava(MavenPublication) {
                from components.java // Publish with standard artifacts
                setGroupId(propertyString('root_package'))// Publish with root package as maven group
                setArtifactId(propertyString('mod_id')) // Publish artifacts with mod id as the artifact id

                // Custom artifact:
                // If you want to publish a different artifact to the one outputted when building normally
                // Create a different gradle task (Jar task), in extra.gradle
                // Remove the 'from components.java' line above
                // Add this line (change the task name):
                // artifacts task_name
            }
        }
    }
}

// Documentation here: https://github.com/matthewprenger/CurseGradle/wiki/
if (propertyBool('publish_to_curseforge')) {
    apply plugin: 'com.matthewprenger.cursegradle'
    assertProperty('curseforge_project_id')
    assertProperty('release_type')
    setDefaultProperty('curseforge_debug', false, false)
    curseforge {
        apiKey = System.getenv('CURSEFORGE_TOKEN') == null ? "" : System.getenv('CURSEFORGE_TOKEN')
        // noinspection GroovyAssignabilityCheck
        project {
            id = propertyString('curseforge_project_id')
            addGameVersion 'Java 8'
            addGameVersion 'Forge'
            addGameVersion '1.12.2'
            releaseType = propertyString('release_type')

            // Use CI-provided changelog if set, otherwise extract from CHANGELOG.md
            def clOverride = propertyString('curseforge_changelog')
            if (!clOverride.isEmpty()) {
                changelog = clOverride
                changelogType = 'markdown'
            } else if (propertyBool('publish_with_changelog')) {
                changelog = parserChangelog()
                changelogType = 'markdown'
            }

            // Use CI-provided display name if set, otherwise default to "Mod Name mod_version"
            def dnOverride = propertyString('curseforge_display_name')
            def displayName = !dnOverride.isEmpty() ? dnOverride : "${propertyString('mod_name')} ${propertyString('mod_version')}"

            mainArtifact tasks.reobfJar, {
                it.displayName = displayName
                if (propertyBool('use_mixins')) {
                    relations {
                        requiredDependency 'mixin-booter'
                    }
                }
                if (propertyBool('use_asset_mover')) {
                    relations {
                        requiredDependency 'assetmover'
                    }
                }

                relations {
                    // Required: AE2-UEL
                    requiredDependency 'ae2-extended-life'
                    // Optional: JEI
                    optionalDependency 'jei'
                    // Optional: Thaumic Energistics (for Essentia cell support)
                    optionalDependency 'thaumic-energistics'
                    // Optional: AE2WUT (Wireless Universal Terminal integration)
                    optionalDependency 'ae2uel-wireless-universal-terminal'
                    // Optional: CrazyAE
                    optionalDependency 'crazyae'
                }
            }
            options {
                debug = propertyBool('curseforge_debug')
            }
        }
    }
}

// Documentation here: https://github.com/modrinth/minotaur
if (propertyBool('publish_to_modrinth')) {
    apply plugin: 'com.modrinth.minotaur'
    assertProperty('modrinth_project_id')
    assertProperty('release_type')
    setDefaultProperty('modrinth_debug', false, false)
    modrinth {
        token = System.getenv('MODRINTH_TOKEN') ? "" : System.getenv('MODRINTH_TOKEN')
        projectId = propertyString('modrinth_project_id')
        versionNumber = propertyString('mod_version')
        versionType = propertyString('release_type')
        uploadFile = tasks.reobfJar
        gameVersions = ['1.12.2']
        loaders = ['forge']
        debugMode = propertyBool('modrinth_debug')
        if (propertyBool('use_mixins') || propertyBool('use_asset_mover')) {
            dependencies {
                if (propertyBool('use_mixins')) {
                    required.project 'mixinbooter'
                }
                if (propertyBool('use_asset_mover')) {
                    required.project 'assetmover'
                }
            }
        }
        if (propertyBool('publish_with_changelog')) {
            changelog = parserChangelog()
        }
        if (propertyBool('modrinth_sync_readme')) {
            syncBodyFrom = file('README.md').text
            tasks.modrinth.dependsOn(tasks.modrinthSyncBody)
        }
    }
}